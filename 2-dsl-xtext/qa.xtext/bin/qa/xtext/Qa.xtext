grammar qa.xtext.Qa with org.eclipse.xtext.common.Terminals
import "platform:/resource/qa/model/qa.ecore" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

QATest:
	'Title: ' title=EString
	(options = QAContainerOptions)?
	(parts += QAPart)*
;

QAContainerOptions: 
	'try' maxTries = INT 'times' (revealAnswer ?= 'reveal' 'correct' 'answer')?
;

QAPart: Question | QASection;

Question:
	text=EString '?' correct=Answer '!'
	('Candidates: ' candidates += Answer (', ' candidates += Answer)*)?
;

QASection:
	'Section ' name=EString '{'
	(options = QAContainerOptions)?
	(questions += Question)*
	'}'
;

Answer: (OptionAnswer | NumberAnswer | TextAnswer | YesNoAnswer);


ExpressionAnswer:
	'=' expression=EString ('+-' epsilon=EDouble)?
;

OptionAnswer:
	'#' optionNumber=EInt
;

NumberAnswer:
	number=EDouble ("+-" epsilon=EDouble)?
	| ExpressionAnswer
;

TextAnswer:
	text=EString
;

YesNoAnswer:
	{YesNoAnswer} (yes ?= 'yes' | 'no')
;


EString returns ecore::EString: STRING;
EDouble returns ecore::EDouble: '-'? INT ('.' INT (('E'|'e') '-'? INT)?)?;
EInt returns ecore::EInt: '-'? INT;
